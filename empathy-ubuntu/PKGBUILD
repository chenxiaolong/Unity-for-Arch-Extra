# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>
# Original Maintainer: Ionut Biru <ibiru@archlinux.org>

# vercheck-pkgbuild: auto
# vercheck-ubuntu: name=${pkgname%-*}, repo=utopic
# vercheck-archlinux: name=${pkgname%-*}, repo=extra, arch=x86_64
# vercheck-gnome: name=${pkgname%-*}, majorver=3.12
# vercheck-ppa: name=${pkgname%-*}, url=ppa:gnome3-team/gnome3-staging

pkgname=empathy-ubuntu
_ppa_rel=0ubuntu1+utopic1
pkgver=3.12.7
pkgrel=1
pkgdesc="A GNOME instant messaging client using the Telepathy framework."
arch=(i686 x86_64)
url="http://live.gnome.org/Empathy"
license=(GPL2)
provides=("empathy=${pkgver}")
conflicts=(empathy)
depends=(clutter-gst clutter-gtk folks gcr iso-codes libcanberra libpulse
         webkitgtk telepathy-farstream telepathy-glib telepathy-logger
         telepathy-mission-control geoclue2 geocode-glib cheese libchamplain
         libunity ido unity-asset-pool)
makedepends=(gnome-common intltool itstool docbook-xsl yelp-tools python2)
optdepends=('telepathy-gabble: XMPP/Jabber support'
            'telepathy-idle: IRC support'
            'telepathy-salut: Link-local XMPP support'
            'telepathy-rakia: SIP support'
            'telepathy-haze: libpurple support')
options=(!makeflags)
groups=(gnome unity-extra)
install=empathy.install
source=("http://ftp.gnome.org/pub/GNOME/sources/${pkgname%-*}/${pkgver%.*}/${pkgname%-*}-${pkgver}.tar.xz"
        "http://ppa.launchpad.net/gnome3-team/gnome3-staging/ubuntu/pool/main/e/empathy/empathy_${_ppa_ver:-${pkgver}}-${_ppa_rel}.debian.tar.xz")
sha512sums=('4c0c6ba19fbf200c6171ccf1935ffb862d1a22ccfd705dff7bac7230ff22330d5244e8330e9a84f8aadaae726384f37b8ebc594d015cab07fdb3bc6386acbc47'
            '601c2d4c71185f8661f6181a0eafa5b002817d316f43b595565f83a60bea9c7ba4723e58802c3fb4b6e067e0a813a155bebeef0e42bdc5ef52c104981dd5b5b6')

prepare() {
  cd "${pkgname%-*}-${pkgver}"

  # Apply Ubuntu's patches
    # Disable patches
      # Do not use Ubuntu Online Accounts
        sed -i '/ubuntu_launch_uoa_panel.patch/d' ../debian/patches/series
        sed -i '/00_linker-fixes.patch/d' ../debian/patches/series

  for i in $(grep -v '#' ../debian/patches/series); do
    patch -p1 -i "../debian/patches/${i}"
  done
}

build() {
  cd "${pkgname%-*}-${pkgver}"

  #autoreconf -vfi

  PYTHON=python2 ./autogen.sh \
    --prefix=/usr \
    --sysconfdir=/etc  \
    --libexecdir=/usr/lib/empathy \
    --disable-static \
    --disable-schemas-compile \
    --enable-gst-1.0=yes \
    --enable-spell=yes \
    --enable-webkit=yes \
    --enable-map=yes \
    --enable-location=yes \
    --enable-geocode=yes \
    --enable-gudev=yes \
    --enable-call-logs=yes \
    --enable-call=yes \
    --enable-ubuntu-online-accounts=no \
    --enable-goa=yes \
    --enable-libunity=yes \
    --enable-control-center-embedding=yes \
    --with-connectivity=nm

  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make -j1
}

package() {
  cd "${pkgname%-*}-${pkgver}"
  make -j1 DESTDIR="${pkgdir}" install

  install -dm755 "${pkgdir}/usr/share/indicators/messages/applications/"
  install -m644 "${srcdir}/debian/indicators/empathy" \
    "${pkgdir}/usr/share/indicators/messages/applications/"
}
