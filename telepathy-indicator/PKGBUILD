# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>

# vercheck-pkgbuild: auto
# vercheck-ubuntu: name=${pkgname}, repo=utopic
# vercheck-launchpad: name=${pkgname}

pkgname=telepathy-indicator
_actual_ver=0.3.1
_extra_ver=+14.10.20140908
pkgver=${_actual_ver}${_extra_ver/\+/.}
pkgrel=1
epoch=1
pkgdesc="Telepathy integration with the messaging menu"
arch=(i686 x86_64)
url="https://launchpad.net/telepathy-indicator"
license=(GPL3)
groups=(unity-extra)
depends=(indicator-messages libunity telepathy-glib)
makedepends=(intltool)
source=("https://launchpad.net/ubuntu/+archive/primary/+files/telepathy-indicator_${_actual_ver}${_extra_ver}.orig.tar.gz"
        0001_autoconf.patch)
sha512sums=('5c562fda9f7b70a4c4a766f8415bd364b0459d3f055c4c919b2bd9d568180161bfc13882a5aa84bed4e026303a786a1cb2acc6c26e862cb85dcceca6991fa8b7'
            'fc3e1b6ae12e6978eae42459e92f40833f6e9a7aa841b7de2b41c8772283aa7cb7f3c2856b0501cd44a1b8cf6b0d4b5f11adfa679b088b70f4ad1cd2e7b4df6c')

prepare() {
  cd "${pkgname}-${_actual_ver}${_extra_ver}"
  patch -p1 -i ../0001_autoconf.patch
}

build() {
  cd "${pkgname}-${_actual_ver}${_extra_ver}"
  autoreconf -vfi
  intltoolize -f
  ./configure --prefix=/usr --sysconfdir=/etc
  make
}

package() {
  cd "${pkgname}-${_actual_ver}${_extra_ver}"
  make DESTDIR="${pkgdir}/" install
}
