# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>

pkgname=libpam-freerdp
_actual_ver=1.0.1
_extra_ver=+13.10.20130724
pkgver=${_actual_ver}${_extra_ver/+/.}
pkgrel=102
pkgdesc="A PAM module that uses FreeRDP to authenticate the account"
arch=('i686' 'x86_64')
url="https://launchpad.net/libpam-freerdp"
license=('GPL')
groups=('unity-extra')
depends=('freerdp')
makedepends=('gnome-common')
options=('!libtool' '!emptydirs')
source=("https://launchpad.net/ubuntu/+archive/primary/+files/libpam-freerdp_${_actual_ver}${_extra_ver}.orig.tar.gz"
        '0001-Disable-tests.patch')
sha512sums=('de8291089dd0c00790e31e71d4e53761512a8f52e01528de3dea1f931426cc2f4e6e5da7930e8ce18b9726f8ee80f6e0dacaa4d8b6d47eae59cd640c89d6f364'
            '47f4799171d6d4b2b29ffa6f5713526489c85e2a69af0b275654045c3ca8d5e475207c6fcbd4288768546da01fec9e1aaf890c6d68fc69c30dfed5dc0063ea29')

prepare() {
  cd "${srcdir}/${pkgname}-${_actual_ver}${_extra_ver}"

  patch -p1 -i "${srcdir}/0001-Disable-tests.patch"
}

build() {
  cd "${srcdir}/${pkgname}-${_actual_ver}${_extra_ver}"

  autoreconf -vfi

  ./configure --prefix=/usr --libexecdir=/usr/lib --disable-static
  make
}

package() {
  cd "${srcdir}/${pkgname}-${_actual_ver}${_extra_ver}"
  make DESTDIR="${pkgdir}/" install

  mv "${pkgdir}"{,/usr}/lib/security/
}

# vim:set ts=2 sw=2 et:
