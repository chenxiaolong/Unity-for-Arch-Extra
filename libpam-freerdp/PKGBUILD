# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>

# vercheck-pkgbuild: auto
# vercheck-ubuntu: name=${pkgname}, repo=utopic
# vercheck-launchpad: name=${pkgname}

pkgname=libpam-freerdp
_actual_ver=1.0.1
_extra_ver=+15.04.20141111
pkgver=${_actual_ver}${_extra_ver/+/.}
pkgrel=1
pkgdesc="A PAM module that uses FreeRDP to authenticate the account"
arch=(i686 x86_64)
url="https://launchpad.net/libpam-freerdp"
license=(GPL)
groups=(unity-extra)
depends=(freerdp)
makedepends=(gnome-common)
options=(!libtool !emptydirs)
source=("https://launchpad.net/ubuntu/+archive/primary/+files/libpam-freerdp_${_actual_ver}${_extra_ver}.orig.tar.gz"
        0001-Disable-tests.patch)
sha512sums=('2f225bff7f44248d13c9f7c285bc61d2b1e1d7bc97a2fd7e92133572b6bbfda769de38631a0d6d3d465ef25c99c27c3608847ca6c49534b1cf94dc439ee011a7'
            '47f4799171d6d4b2b29ffa6f5713526489c85e2a69af0b275654045c3ca8d5e475207c6fcbd4288768546da01fec9e1aaf890c6d68fc69c30dfed5dc0063ea29')

prepare() {
  cd "${pkgname}-${_actual_ver}${_extra_ver}"

  patch -p1 -i ../0001-Disable-tests.patch
}

build() {
  cd "${pkgname}-${_actual_ver}${_extra_ver}"

  autoreconf -vfi

  ./configure --prefix=/usr --libexecdir=/usr/lib --disable-static
  make
}

package() {
  cd "${pkgname}-${_actual_ver}${_extra_ver}"
  make DESTDIR="${pkgdir}/" install

  mv "${pkgdir}"{,/usr}/lib/security/
}
