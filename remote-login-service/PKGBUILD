# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>

# vercheck-pkgbuild: auto
# vercheck-ubuntu: name=${pkgname}, repo=utopic
# vercheck-launchpad: name=${pkgname}

pkgname=remote-login-service
_ubuntu_rel=0ubuntu3
pkgver=1.0.0
pkgrel=1
epoch=1
pkgdesc="Service to track the remote servers to use"
arch=(i686 x86_64)
url="http://launchpad.net/remote-login-service/"
license=(GPL)
groups=(unity-extra)
depends=(dbus json-glib libsoup networkmanager python thin-client-config-agent)
makedepends=(intltool)
source=("https://launchpad.net/remote-login-service/1.0/${pkgver}/+download/remote-login-service-${pkgver}.tar.gz"
        "https://launchpad.net/ubuntu/+archive/primary/+files/remote-login-service_${pkgver}-${_ubuntu_rel}.debian.tar.gz"
        0001_Disable_tests.patch)
sha512sums=('a0e93d9c599c00abf382b63f282f59fb27f40ac6699035b07f87cd1bf81ffc51b3daced1dd32e506e4bcb1bf2d455a940ec1550ffb72e1c3fff1a494fbdec6af'
            '351e1bf70e167d45d0aa2689d51cb082cacf27e5ee40af510fc956a759f9dd08426b847135a4dac3bed0ca8375e93f847f53f07d6c764fff8e737dbd52106137'
            '7a4ce2e734f96a0189bdb8068b5447cce87bf03a8edc2359d1ca25bcd3323e207d60212639c62eeb4aa8ca0259cfd4e88ba90c51fa13d75b42aba10e7dba3f03')

prepare() {
  cd "${pkgname}-${pkgver}"

  patch -p1 -i ../0001_Disable_tests.patch

  # Apply Ubuntu's patches
  for i in $(grep -v '#' ../debian/patches/series); do
    patch -p1 -i "../debian/patches/${i}"
  done
}

build() {
  cd "${pkgname}-${pkgver}"

  export CFLAGS+=" -Wno-error"

  autoreconf -vfi

  ./configure --prefix=/usr --libexecdir=/usr/lib --sysconfdir=/etc
  make
}

package() {
  cd "${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}/" install
}
