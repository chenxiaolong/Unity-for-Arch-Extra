# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>

# vercheck-pkgbuild: auto
# vercheck-ubuntu: name=${pkgname}, repo=yakkety, native

_ubuntu_ver=0.0.7ubuntu2

pkgname=unity-tweak-tool
pkgver=0.0.7
pkgrel=1
pkgdesc="Configuration tool for the Unity desktop"
arch=(any)
url="https://github.com/freyja-dev/unity-tweak-tool"
license=(GPL)
groups=(unity-extra)
depends=(gtk3 hicolor-icon-theme notify-osd python python-xdg python-dbus
         unity)
makedepends=(intltool python-distutils-extra)
options=(!emptydirs)
source=("https://launchpad.net/ubuntu/+archive/primary/+files/unity-tweak-tool_${_ubuntu_ver}.tar.gz"
        0001-Remove-Unity-WebApps-support.patch
        0002-Remove-indicator-bluetooth-support.patch)
sha512sums=('6d5f682e9c8c9f8f172c2a102adffccc04fa944c0fe7acc382fc14dceafe74f498e63c935e273c92bf553100f8096f2f89ee84cb939fbfa0dc548ba1f8ef7509'
            '32b00babc7f899f280728160a55e633c85fb3199b7b988352c4f79ed0788e90c9cda9034f8238f372cb4e4cbe7076974149f56f835cb30d8da9a7505d899f179'
            'a4efc29bd3621c8f70bcdfaba486ccbbcd3fabd72d93a9f71023f2586eedfa2a29e3ab41e96b7c0ad705974f6911d4a0e25c78d068b1622f151c5e4f58ea3c20')

prepare() {
    cd "${pkgname}-${_ubuntu_ver}"
    patch -p1 -i ../0001-Remove-Unity-WebApps-support.patch
    patch -p1 -i ../0002-Remove-indicator-bluetooth-support.patch
}

package() {
    cd "${pkgname}-${_ubuntu_ver}"
    python setup.py install --root="${pkgdir}/" --optimize=1

    rm -v "${pkgdir}"/usr/share/unity-tweak-tool/media/48/unity-settings-webapps.svg
}
