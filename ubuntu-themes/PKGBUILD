# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>

# vercheck-pkgbuild: auto
# vercheck-ubuntu: name=${pkgname}, repo=xenial

pkgname=ubuntu-themes
_actual_ver=14.04
_extra_ver=+16.04.20160415
pkgver=${_actual_ver}${_extra_ver/\+/.}
pkgrel=1
pkgdesc="Ubuntu monochrome icon theme, Ambiance and Radiance themes, and Ubuntu artwork"
arch=(any)
url="https://launchpad.net/ubuntu-themes"
license=(CCPL)
groups=(unity-extra)
depends=(humanity-icon-theme adwaita-icon-theme hicolor-icon-theme
         gtk-engine-murrine)
makedepends=(python2 imagemagick icon-naming-utils)
provides=(light-themes ubuntu-mono)
conflicts=(light-themes ubuntu-mono)
source=("https://launchpad.net/ubuntu/+archive/primary/+files/${pkgname}_${_actual_ver}${_extra_ver}.orig.tar.gz")
sha512sums=('05801c6b38db0a0f2587014949be4fb55e6925bf1099c76f506f3b8560b39bfa2758f9d65068500b2b4b58c20946a1b032215176646c888154c2a35c973efe65')

build() {
    #cd "${pkgname}-${_actual_ver}${_extra_ver}"

    # Python 2 fix
    sed -i 's|^\(#!.*python$\)|\12|g' scavenge.py

    make
}

package() {
    #cd "${pkgname}-${_actual_ver}${_extra_ver}"

    # Install themes
    install -dm755   "${pkgdir}/usr/share/themes/"
    cp -av Ambiance/ "${pkgdir}/usr/share/themes/"
    cp -av Radiance/ "${pkgdir}/usr/share/themes/"

    # Install icons
    install -dm755            "${pkgdir}/usr/share/icons/"
    cp -av ubuntu-mono-dark/  "${pkgdir}/usr/share/icons/"
    cp -av ubuntu-mono-light/ "${pkgdir}/usr/share/icons/"
    cp -av LoginIcons/        "${pkgdir}/usr/share/icons/"
    cp -av suru-icons/        "${pkgdir}/usr/share/icons/"
    cp -av ubuntu-mobile/     "${pkgdir}/usr/share/icons/"

    mv "${pkgdir}"/usr/share/icons/{suru-icons,suru}/

    install -Dm644 distributor-logo.png \
        "${pkgdir}/usr/share/icons/hicolor/48x48/apps/distributor-logo.png"
}
