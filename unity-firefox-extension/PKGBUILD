# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>

# Upstream package name is unity-firefox-extension

pkgname=firefox-extension-unity
pkgver=2.3.5
pkgrel=100
pkgdesc="Firefox extension for Unity integration"
arch=('i686' 'x86_64')
url="https://launchpad.net/unity-firefox-extension"
license=('GPL')
groups=('unity-extra')
depends=('firefox-extension-websites-integration' 'libunity-webapps')
makedepends=('gtk2' 'libxslt' 'zip')
options=('!libtool')
source=("https://launchpad.net/unity-firefox-extension/trunk/${pkgver}/+download/unity-firefox-extension-${pkgver}.tar.gz"
        'launcher_bfb.png')
sha512sums=('996a9f20c0ad036b51ff895190356f6fb21580dc6f9137cefd3794027adb328323f555e1311274e042146b75fac41e82a3ec237e5b2d2e11be9c7f9725ffa256'
            '9f5469c0300d0c33e26db670771803efd3d821e82689fbb1391a31b8fd0e280a12ea010fe6f32a702a5d1797918d416c8adabfccf6049e43b496ebb6dbe2c054')

build() {
  cd "${srcdir}/unity-firefox-extension-${pkgver}"

  pushd libufe-xidgetter/
  autoreconf -vfi
  ./configure --prefix=/usr --disable-static
  make ${MAKEFLAGS}
  popd

  pushd unity-firefox-extension/
  bash ./build.sh
  popd
}

package() {
  cd "${srcdir}/unity-firefox-extension-${pkgver}"

  pushd libufe-xidgetter/
  make DESTDIR="${pkgdir}/" install
  popd

  pushd unity-firefox-extension/
  local emid=$(sed -n 's/.*<em:id>\(.*\)<\/em:id>.*/\1/p' install.rdf | head -1)
  install -dm755 "${pkgdir}/usr/lib/firefox/extensions/${emid}/"
  unzip unity.xpi -d "${pkgdir}/usr/lib/firefox/extensions/${emid}/"
  popd

  # Arch Linux logo
  rm "${pkgdir}/usr/lib/firefox/extensions/${emid}/skin/cof.png"
  install -m644 "${srcdir}/launcher_bfb.png" \
    "${pkgdir}/usr/lib/firefox/extensions/${emid}/skin/cof.png"
}

# vim:set ts=2 sw=2 et:
