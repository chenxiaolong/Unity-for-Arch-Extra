# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>

# vercheck-pkgbuild: auto
# vercheck-ubuntu: name=${pkgname}, repo=utopic, native
# vercheck-launchpad: name=humanity, tarname=Humanity

pkgname=humanity-icon-theme
pkgver=0.6.5
pkgrel=1
pkgdesc="Humanity Icon Theme"
arch=(any)
url="https://launchpad.net/humanity"
license=(GPL)
groups=(unity-extra)
depends=(gnome-icon-theme hicolor-icon-theme)
makedepends=(icon-naming-utils)
provides=("humanity-icons=${pkgver}")
conflicts=(humanity-icons)
install=${pkgname}.install
source=("https://launchpad.net/ubuntu/+archive/primary/+files/${pkgname}_${pkgver}.tar.gz")
sha512sums=('d5c20abef577ee09639f371a235d2213a3c17fdf97348d0d42ecdb8d690fc8fc80725e9499b96ae4fa59c44cc5622cec2c24e4becf8b85fafbf6cf64c786695c')

package() {
  cd "${pkgname}-${pkgver}"

  install -dm755 "${pkgdir}"/usr/share/icons/

  # Install Humanity icon theme
  cp -a Humanity/ "${pkgdir}"/usr/share/icons/

  # Install Humanity-Dark icon theme
  cp -a Humanity-Dark/ "${pkgdir}"/usr/share/icons/

  # From debian/rules
  for i in $(find "${pkgdir}/usr/share/icons/" -mindepth 2 -maxdepth 2 -type d); do
    (
    cd "${i}"
    for j in *; do
      /usr/lib/icon-naming-utils/icon-name-mapping -c "${j}"
    done
    )
  done
}
