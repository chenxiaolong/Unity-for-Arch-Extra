# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>

# vercheck-pkgbuild: auto
# vercheck-ubuntu: name=${pkgname}, repo=vivid, native
# vercheck-launchpad: name=humanity, tarname=Humanity

pkgname=humanity-icon-theme
pkgver=0.6.6
pkgrel=1
pkgdesc="Humanity Icon Theme"
arch=(any)
url="https://launchpad.net/humanity"
license=(GPL)
groups=(unity-extra)
depends=(gnome-icon-theme hicolor-icon-theme)
makedepends=(icon-naming-utils)
provides=("humanity-icons=${pkgver}")
conflicts=(humanity-icons)
install=${pkgname}.install
source=("https://launchpad.net/ubuntu/+archive/primary/+files/${pkgname}_${pkgver}.tar.gz")
sha512sums=('f96195332e5ef5e0ec51b17cb69b87c8cf7fb6fbe851e32c0bad900b09a6602517904d09fbadeae62bb15972c5b1e49d5f1d132c637780cdf7fa15dd40717d12')

package() {
  cd "${pkgname}-${pkgver}"

  install -dm755 "${pkgdir}"/usr/share/icons/

  # Install Humanity icon theme
  cp -a Humanity/ "${pkgdir}"/usr/share/icons/

  # Install Humanity-Dark icon theme
  cp -a Humanity-Dark/ "${pkgdir}"/usr/share/icons/

  # From debian/rules
  for i in $(find "${pkgdir}/usr/share/icons/" -mindepth 2 -maxdepth 2 -type d); do
    (
    cd "${i}"
    for j in *; do
      /usr/lib/icon-naming-utils/icon-name-mapping -c "${j}"
    done
    )
  done
}
