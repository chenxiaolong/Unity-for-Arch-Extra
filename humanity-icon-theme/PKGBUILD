# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>

# vercheck-pkgbuild: auto
# vercheck-ubuntu: name=${pkgname}, repo=xenial, native
# vercheck-launchpad: name=humanity, tarname=Humanity

pkgname=humanity-icon-theme
pkgver=0.6.10
pkgrel=2
pkgdesc="Humanity Icon Theme"
arch=(any)
url="https://launchpad.net/humanity"
license=(GPL)
groups=(unity-extra)
depends=(adwaita-icon-theme hicolor-icon-theme)
makedepends=(icon-naming-utils)
provides=("humanity-icons=${pkgver}")
conflicts=(humanity-icons)
source=("https://launchpad.net/ubuntu/+archive/primary/+files/${pkgname}_${pkgver}.tar.gz")
sha512sums=('4b224871e7423c47a614f9e340c00b48a03553da36707fc38b9a03c01bf29df67793c9f6cb3dcdfda68e8de396c8e71d8824f1c1476c7be5bd870b64a26fb51d')

package() {
    cd "${pkgname}-${pkgver}"

    install -dm755 "${pkgdir}"/usr/share/icons/

    # Install Humanity icon theme
    cp -a Humanity/ "${pkgdir}"/usr/share/icons/

    # Install Humanity-Dark icon theme
    cp -a Humanity-Dark/ "${pkgdir}"/usr/share/icons/

    # From debian/rules
    for i in $(find "${pkgdir}/usr/share/icons/" -mindepth 2 -maxdepth 2 -type d); do
        (
        cd "${i}"
        for j in *; do
            /usr/lib/icon-naming-utils/icon-name-mapping -c "${j}"
        done
        )
    done
}
