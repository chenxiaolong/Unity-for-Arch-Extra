# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>

# vercheck-pkgbuild: auto
# vercheck-ubuntu: name=${pkgname}, repo=zesty, native
# vercheck-launchpad: name=humanity, tarname=Humanity

pkgname=humanity-icon-theme
pkgver=0.6.11
pkgrel=1
pkgdesc="Humanity Icon Theme"
arch=(any)
url="https://launchpad.net/humanity"
license=(GPL)
groups=(unity-extra)
depends=(adwaita-icon-theme hicolor-icon-theme)
makedepends=(icon-naming-utils)
provides=("humanity-icons=${pkgver}")
conflicts=(humanity-icons)
source=("https://launchpad.net/ubuntu/+archive/primary/+files/${pkgname}_${pkgver}.tar.xz")
sha512sums=('28b018f972e02167a2da8859b0c7fb41d1f4abaa18e6d56fb4ed4f21db8b12c4ec32190e74b14343bcd3f6ffae6facc06abe3ce4f1effd805d65d0debabfcab5')

package() {
    cd "${pkgname}-${pkgver}"

    install -dm755 "${pkgdir}"/usr/share/icons/

    # Install Humanity icon theme
    cp -a Humanity/ "${pkgdir}"/usr/share/icons/

    # Install Humanity-Dark icon theme
    cp -a Humanity-Dark/ "${pkgdir}"/usr/share/icons/

    # From debian/rules
    for i in $(find "${pkgdir}/usr/share/icons/" -mindepth 2 -maxdepth 2 -type d); do
        (
        cd "${i}"
        for j in *; do
            /usr/lib/icon-naming-utils/icon-name-mapping -c "${j}"
        done
        )
    done
}
